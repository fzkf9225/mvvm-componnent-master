plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.ksp)
}

android {
    namespace = 'com.casic.otitan.commonui'
    compileSdk = libs.versions.compileSdkVersion.get().toInteger()
    defaultConfig {
        minSdk = libs.versions.minSdkVersion.get().toInteger()
        targetSdk = libs.versions.targetSdkVersion.get().toInteger()

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        ndk {
            //armeabi-v7a 较旧的 Android 设备（大约 2016年前），逐渐淘汰，32位架构
            //armeabi-v8a 绝大多数现代 Android 手机和平板,主流和未来,64位架构
            //x86 旧的 Android 模拟器和极少数平板，基本淘汰，32位架构
            //x86_64 少数 Android 平板、Chromebook 和模拟器，小众但重要，64位架构
            abiFilters = ['armeabi-v7a', 'armeabi-v8a', 'x86', 'x86_64']
//            abiFilters 'armeabi', 'armeabi-v7a', 'armeabi-v8a',
//                    'x86', 'x86_64', 'mips', 'mips64'
        }
    }

    buildTypes {
        release {
            resValue("string", "app_config_file", "prod.properties")
            buildConfigField("boolean", "LOG_DEBUG", "false")// 不显示log
            minifyEnabled = false// 混淆
            proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')
            //新版混淆添加以下代码：，如果只添加上面会导致混淆规则一直无效；
            consumerProguardFiles('consumer-rules.pro')
        }
        debug {
            resValue("string", "app_config_file", "dev.properties")
            buildConfigField("boolean", "LOG_DEBUG", "true")// 显示log
            minifyEnabled = false// 混淆
            proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')
        }
    }
    compileOptions {
        encoding = "UTF-8"
        targetCompatibility = JavaVersion.VERSION_17
        sourceCompatibility = JavaVersion.VERSION_17
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
    ksp {
        arg("room.schemaLocation", "$projectDir/schemas")
    }
    kotlin {
        jvmToolchain(17)  // 这是 kotlin DSL 的写法
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }
    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding = true
        buildConfig = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation libs.androidx.core.ktx
    implementation platform(libs.kotlin.bom)
    implementation libs.androidx.appcompat
    implementation libs.viewpager2
    implementation libs.material
    implementation libs.constraintlayout
    implementation libs.androidx.recyclerview
    implementation libs.github.glide
    implementation libs.reactivex.rxandroid
    implementation libs.rxjava3.rxjava
    implementation libs.gson
    implementation project(':common')
    implementation project(':commonmedia')

    implementation libs.androidx.room.ktx
    implementation libs.androidx.room.runtime
    ksp libs.room.compiler
    // optional - RxJava3 support for Room
    implementation libs.room.rxjava3
    // optional - Paging 3 Integration
    implementation libs.room.paging

    //上传文件使用
    implementation libs.okhttp
    implementation libs.retrofit
    implementation libs.reactivex.rxandroid
    implementation libs.rxjava3.rxjava
    //ConverterFactory的String依赖包
    implementation libs.retrofit2.converter.scalars
    //ConverterFactory的Gson依赖包
    implementation libs.retrofit2.converter.gson
    //CallAdapterFactory的Rx依赖包
    implementation libs.retrofit2.adapter.rxjava3
}

//执行以下命令完成制品推送
//推送命令
//gradle publish
//# 如果你的项目使用了gradle wrapper组件的话请使用以下命令
//# ./gradlew publish
apply plugin: 'maven-publish'

// 任务注册：生成源代码 JAR
tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set("sources") // 设置 classifier 为 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar // 将源码 JAR 添加到 artifacts
}

afterEvaluate {
    publishing {
        publications {
            plugin(MavenPublication) {
                // 发布源码 JAR，确保 classifier 为 'sources'，并且 extension 为 'jar'
                artifact(androidSourcesJar)
                artifact("${buildDir}/outputs/aar/${artifactId}-release.aar")
                groupId = 'com.casic.otitan.commonui'
                artifactId = 'commonui'
                version = '2.0.7'
            }
        }
        repositories {
            maven {
                url = 'https://packages.aliyun.com/maven/repository/2405978-release-ObRSGq/'
                credentials {
                    username = System.getenv("ALIYUN_USER_NAME")
                    password = System.getenv("ALIYUN_PASSWORD")
                }
            }
            maven {
                url = 'https://packages.aliyun.com/maven/repository/2405978-snapshot-rT6GM2/'
                credentials {
                    username = System.getenv("ALIYUN_USER_NAME")
                    password = System.getenv("ALIYUN_PASSWORD")
                }
            }
        }
    }
}